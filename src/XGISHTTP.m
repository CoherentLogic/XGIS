XGISHTTP ;CLD/JOLLIS - Spatial Extensions GeoJSON Server;01 May 2013
;;1.0;XGIS;**PATCHES**;01 May 2013;Build 1

SERVER()
 N REQ,DEL,RESOURCE,API,RESP,ARGC,ARGV,SUCCESS,URL S URL="/"
 R REQ
 ; set up the resource
 S RESOURCE=$P(REQ,"HTTP/",1) S ARGC=$L(RESOURCE,"/")
 N ARGIDX S ARGIDX=1
 F I=2:1:ARGC S ARGV(ARGIDX)=$P(RESOURCE,"/",I),ARGIDX=ARGIDX+1
 S API=ARGV(1) 
 S SUCCESS=0
 I API="feature" D
 . D NEWRESP(.RESP,"application/json",200,"OK")
 . D ADDBODY(.RESP,$$FEATURE^XGISJSON($P(RESOURCE,"/",3)))
 . D FINALIZE(.RESP)
 . D SEND(.RESP)
 . S SUCCESS=1
 I API="resource" D
 . F I=2:1:ARGC-2 S URL=URL_ARGV(I)_"/"
 . N RSRCIEN,OUT S OUT=0 S RSRCIEN=$$WEBRES^XGISDI(URL,.OUT)
 . I RSRCIEN=0 S SUCCESS=0 G NOGO
 . I RSRCIEN'=0 S SUCCESS=1
 . N CTYPE S CTYPE=OUT(5.8,"1,","CONTENT TYPE")
 . D NEWRESP(.RESP,CTYPE,200,"OK")
 . N WPLINE S WPLINE=""
 . F I=1:1 S WPLINE=$O(OUT(5.8,"1,","BODY",WPLINE)) Q:WPLINE=""  D
 . . D ADDBODY(.RESP,OUT(5.8,"1,","BODY",WPLINE))
 . D FINALIZE(.RESP)
 . D SEND(.RESP) 
NOGO
 I SUCCESS=0 D ERROR(404)
 Q

ERROR(ERRNUM)
 N RESP D NEWRESP(.RESP,"text/html",ERRNUM,^XGISUTL("SERVER","ERRORS",ERRNUM,"MESSAGE"))
 D ADDBODY(.RESP,^XGISUTL("SERVER","ERRORS",ERRNUM,"BODY"))
 D FINALIZE(.RESP)
 D SEND(.RESP)
 HALT
 Q

NEWRESP(OA,CONTTYPE,STATVAL,STATMSG)
 S OA("LENGTH")=0
 S OA("BODY LINES")=0
 S OA("HEADER LINES")=0
 S OA("CONTENT-TYPE")=CONTTYPE
 S OA("STATUS-VALUE")=STATVAL
 S OA("STATUS-MESSAGE")=STATMSG
 S OA("BODY")=""
 D ADDRESP(.OA,$$HTTPRESP(STATVAL,STATMSG))
 D ADDHDR(.OA,$$HDR("Content-Type",CONTTYPE))
 D ADDHDR(.OA,$$HDR("server","VistA HTTPD 0.1"))
 Q

ADDRESP(OA,VALUE)
 S OA("RESPONSE")=VALUE
 Q

ADDHDR(OA,VALUE)
 S OA("HEADER LINES")=OA("HEADER LINES")+1
 S OA("HEADER",OA("HEADER LINES"))=VALUE
 Q 

ADDBODY(OA,VALUE)
 S OA("LENGTH")=OA("LENGTH")+$L(VALUE)+2
 S OA("BODY LINES")=OA("BODY LINES")+1
 S OA("BODY",OA("BODY LINES"))=VALUE
 Q

FINALIZE(OA)
 D ADDHDR(.OA,$$HDR("Content-Length",OA("LENGTH")))
 Q

SEND(OA)
 N DEL S DEL=$C(13,10)
 W OA("RESPONSE"),DEL
 F I=1:1:OA("HEADER LINES")  D
 . W OA("HEADER",I),DEL
 W DEL
 F I=1:1:OA("BODY LINES")  D
 . W OA("BODY",I),DEL
 Q 

HTTPRESP(STATUS,MSG)
 Q "HTTP/1.1 "_STATUS_" "_MSG
 

HDR(NAME,VALUE)
 Q NAME_": "_VALUE